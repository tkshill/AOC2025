{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "1cf0b689",
   "metadata": {},
   "source": [
    "# Advent of Code 2025"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "95199f29",
   "metadata": {
    "language_info": {
     "name": "polyglot-notebook"
    },
    "polyglot_notebook": {
     "kernelName": "fsharp"
    }
   },
   "outputs": [],
   "source": [
    "open FSharp.Collections\n",
    "open System\n",
    "\n",
    "let trueMod n x =\n",
    "      x % n |> (function | rem when rem < 0  -> rem + n | rem -> rem)\n",
    "\n",
    "let turn current (change: string) = \n",
    "    int (change.Substring(1))\n",
    "    |> (if change[0] = 'R' then (+) else (-)) current \n",
    "    |> trueMod 100 \n",
    "\n",
    "let part1 = Seq.scan turn 50 \n",
    "    \n",
    "let tick direction = function\n",
    "    | _, 0                  -> None\n",
    "    | current, remaining    ->\n",
    "        if direction = 'R' then (current + 1) % 100 \n",
    "        else trueMod 100 (current - 1 + 100)\n",
    "        |> fun next -> Some (next, (next, remaining - 1))\n",
    "\n",
    "let turns (head::rest) (change: string) = \n",
    "    List.unfold (tick change[0]) (head, int (change.Substring(1)))\n",
    "    |> List.rev\n",
    "    |> fun seq -> seq @ (head :: rest)\n",
    "\n",
    "let part2 = Seq.fold turns [50]\n",
    "\n",
    "let run filePath part =\n",
    "    System.IO.File.ReadAllLines(filePath) \n",
    "    |> part\n",
    "    |> Seq.sumBy (function | 0 -> 1 | _ -> 0)\n",
    "\n",
    "run \"day1.txt\" part1\n",
    "run \"day1.txt\" part2"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c14352b2",
   "metadata": {},
   "source": [
    "## Day 2"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "language_info": {
     "name": "polyglot-notebook"
    },
    "polyglot_notebook": {
     "kernelName": "fsharp"
    }
   },
   "outputs": [],
   "source": [
    "#r \"nuget: FParsec, 1.1.1\"\n",
    "#r \"nuget: FParsec-Pipes, 1.1.2\"\n",
    "#r \"nuget: FSharpx.Collections, 3.1.0\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "language_info": {
     "name": "polyglot-notebook"
    },
    "polyglot_notebook": {
     "kernelName": "fsharp"
    }
   },
   "outputs": [],
   "source": [
    "open FParsec\n",
    "open FParsec.Pipes\n",
    "open FSharpx.Collections\n",
    "\n",
    "let runParser parser input =\n",
    "    match run parser input with\n",
    "    | Success(result, _, _)     -> result\n",
    "    | Failure(errorMsg, _, _)   -> failwith errorMsg\n",
    "\n",
    "let inputParser = \n",
    "    let pRange = %% +.pint64 -- '-' -- +.pint64 -%> auto\n",
    "    %% +.(qty[1..] / ',' * pRange) -%> ResizeArray.toSeq\n",
    "\n",
    "let isInvalid (str: char array) = \n",
    "    Seq.length str |> fun l -> str[0..(l/2)-1] = str[l/2..l-1]\n",
    "\n",
    "let findInvalids checker (a, b) =\n",
    "    seq { for i in a..b do if checker (Seq.toArray (string i)) then yield i } \n",
    "\n",
    "let part1 = Seq.sumBy (findInvalids isInvalid >> Seq.sum)\n",
    "\n",
    "let rec isInvalid2 (str: char array) = function\n",
    "    | 0                                                                             -> false\n",
    "    | shift when Array.permute (fun j -> (j + shift) % Seq.length str) str = str    -> true\n",
    "    | shift                                                                         -> isInvalid2 str (shift - 1)\n",
    "\n",
    "let part2 = \n",
    "    let wrapper chars = isInvalid2 chars (Seq.length chars / 2)\n",
    "    Seq.sumBy (findInvalids wrapper >> Seq.sum)\n",
    "\n",
    "let run filePath part =\n",
    "    System.IO.File.ReadAllText(filePath) \n",
    "    |> runParser inputParser\n",
    "    |> part\n",
    "\n",
    "run \"day2.txt\" part1\n",
    "run \"day2.txt\" part2\n",
    "  "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c4e279a8",
   "metadata": {},
   "source": [
    "## Day 3"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "language_info": {
     "name": "polyglot-notebook"
    },
    "polyglot_notebook": {
     "kernelName": "fsharp"
    }
   },
   "outputs": [],
   "source": [
    "let rec largest2 collected remaining = \n",
    "    match collected, remaining with\n",
    "    | _         , []                                        -> collected\n",
    "    | []        , head  :: next :: rest                     -> largest2 [head   ; next] rest\n",
    "    | [fst; snd], head  :: rest          when snd  > fst    -> largest2 [snd    ; head] rest\n",
    "    | [fst; _]  , head  :: next :: rest  when head > fst    -> largest2 [head   ; next] rest\n",
    "    | [fst; snd], head  :: rest          when head > snd    -> largest2 [fst    ; head] rest\n",
    "    | _         , _     :: rest                             -> largest2 collected       rest\n",
    "\n",
    "let charToInt c = int c - int '0'\n",
    "\n",
    "// For largest 12 digits\n",
    "\n",
    "let (|CanShiftAtIndex|_|) (collected, ((head :: rest) as remaining)) =\n",
    "    List.mapi (fun i x -> (i, x < head && List.length remaining >= (12 - i))) collected\n",
    "    |> List.tryFind snd\n",
    "    |> Option.map fst\n",
    "\n",
    "let rec largest12 collected remaining =\n",
    "    match collected, remaining with\n",
    "    | _, []             -> collected |> List.take 12\n",
    "    | [], _             -> largest12 [remaining[0]]                         remaining[1..]\n",
    "    | CanShiftAtIndex i -> largest12 (collected[0..(i-1)] @ [remaining[0]]) remaining[1..]\n",
    "    | _, head :: rest   -> largest12 (collected @ [head])                   rest\n",
    "\n",
    "let run filePath (solver: int list -> int list -> int list)  =\n",
    "    System.IO.File.ReadAllLines(filePath)\n",
    "    |> Seq.sumBy (Seq.toList >> List.map charToInt >> solver [] >> String.Concat >> int64)\n",
    "\n",
    "run \"day3.txt\" largest2 // part 1\n",
    "run \"day3.txt\" largest12 // part 2\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c23231f6",
   "metadata": {},
   "source": [
    "## Day 4"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "language_info": {
     "name": "polyglot-notebook"
    },
    "polyglot_notebook": {
     "kernelName": "fsharp"
    }
   },
   "outputs": [],
   "source": [
    "let flip f a b = f b a\n",
    "\n",
    "let parseInput (lines: string array) =\n",
    "    seq { for i in 0 .. lines.Length - 1 do for j in 0 .. lines[i].Length - 1 do (i, j), lines[i][j] }\n",
    "    |> Map.ofSeq\n",
    "\n",
    "let bloom (map: Map<int * int, char>) (i, j) =\n",
    "    [ (i - 1, j); (i + 1, j); (i, j - 1); (i, j + 1); (i - 1, j - 1); (i - 1, j + 1); (i + 1, j - 1); (i + 1, j + 1) ]\n",
    "    |> List.filter (flip Map.tryFind map >> (=) (Some '@'))\n",
    "    |> List.length\n",
    "\n",
    "let findAccessibles map =\n",
    "    Map.filter (fun (i, j) v -> v = '@' && bloom map (i, j) < 4) map\n",
    "    |> Map.count\n",
    "\n",
    "let findAccessibles2 map =\n",
    "    Map.filter (fun (i, j) v -> not (v = '@' && bloom map (i, j) < 4)) map\n",
    "    |> function\n",
    "        | iteration when Map.count iteration = Map.count map    -> None\n",
    "        | iteration                                             -> Some (Map.count map - Map.count iteration, iteration)\n",
    "\n",
    "let run filePath solver = \n",
    "    System.IO.File.ReadAllLines(filePath)\n",
    "    |> parseInput\n",
    "    |> solver\n",
    "     \n",
    "run \"day4.txt\" findAccessibles // part 1\n",
    "run \"day4.txt\" (Seq.unfold findAccessibles2 >> Seq.sum) // part 2"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "62ff967b",
   "metadata": {},
   "source": [
    "## Day 5"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e0f1c0d6",
   "metadata": {
    "language_info": {
     "name": "polyglot-notebook"
    },
    "polyglot_notebook": {
     "kernelName": "fsharp"
    }
   },
   "outputs": [],
   "source": [
    "let curry f a b = f (a, b)\n",
    "\n",
    "let parse (input: string) =\n",
    "    let rs :: ns :: _ = input.Split(\"\\n\\n\") |> Array.toList\n",
    "    let ranges = \n",
    "        rs.Split('\\n')\n",
    "        |> Seq.map (fun line -> \n",
    "            let parts = line.Split('-')\n",
    "            int64 parts[0], int64 parts[1])\n",
    "    let numbers = ns.Split('\\n') |> Seq.map int64\n",
    "\n",
    "    ranges, numbers\n",
    "\n",
    "let isValid ranges n =\n",
    "    Seq.exists (fun (low, high) -> n >= low && n <= high) ranges\n",
    "\n",
    "let sieve (ranges, numbers) =\n",
    "    numbers \n",
    "    |> Seq.filter (isValid ranges) \n",
    "    |> Seq.length\n",
    "\n",
    "let tryMerge (l1, h1) (l2, h2) =\n",
    "    if h1 < l2 || h2 < l1 \n",
    "    then [ (l1, h1); (l2, h2) ] \n",
    "    else [min l1 l2, max h1 h2]\n",
    "\n",
    "let rec rangeTraverser tested = function\n",
    "    | []                        ,   nextRange -> nextRange :: tested\n",
    "    | first :: restToCompare    ,   nextRange ->\n",
    "        tryMerge first nextRange\n",
    "        |> function\n",
    "            | [merged] -> rangeTraverser tested             (restToCompare, merged)\n",
    "            | [r1; r2] -> rangeTraverser (first :: tested)  (restToCompare, nextRange)\n",
    "\n",
    "let sieve2 (ranges: (int64 * int64) list) =\n",
    "    Seq.fold (fun rangesToCompare initialRange -> rangeTraverser [] (rangesToCompare, initialRange)) [ranges.Head] ranges.Tail\n",
    "    |> Seq.sumBy (fun (low, high) -> high - low + 1L)\n",
    "\n",
    "let run filePath solver =\n",
    "    System.IO.File.ReadAllText(filePath) \n",
    "    |> parse \n",
    "    |> solver\n",
    "\n",
    "run \"day5.txt\" sieve\n",
    "run \"day5.txt\" (fst >> Seq.toList >> sieve2)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": ".NET (C#)",
   "language": "C#",
   "name": ".net-csharp"
  },
  "language_info": {
   "name": "polyglot-notebook"
  },
  "polyglot_notebook": {
   "kernelInfo": {
    "defaultKernelName": "csharp",
    "items": [
     {
      "aliases": [],
      "name": "csharp"
     }
    ]
   }
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
